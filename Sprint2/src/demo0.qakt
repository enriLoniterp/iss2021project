System carparking

//parkingmanagerservice-trolleycontroller interaction
Dispatch moveToIn : moveToIn(MOVETOIN)
Dispatch movedToIn : movedToIn(MOVED)

Dispatch moveToSlot : moveToSlot(X)
Dispatch movedToSlot : movedToSlot(SUCCESS,X,Y)

Dispatch moveToHome : moveToHome(X) 
Dispatch movedToHome : movedToHome(Y)

Dispatch moveToOut : moveToOut(X)
Dispatch movedToOut : movedToOut(SUCCESS)

Dispatch stop : stop(STOP)
Dispatch resume : resume(RESUME)

Dispatch error : error(ERROR)

Dispatch goToIdle : goToIdle(X)
Dispatch stop	 : stop(X)
Dispatch resume : resume(X)	
Dispatch cmd       : cmd(MOVE)    
Request step       : step( TIME )	

Dispatch command       : command( X )
//Reply executed : executed(STATE)


Context ctxcarparking ip[host="localhost" port=8002]



QActor trolley_controller context  ctxcarparking{
	[#
		var currentTask : String = "NONE"
		lateinit var mv : String
		lateinit var ttAd : ActuatorPort
		val HOME : Pair<String,String> = Pair("0", "0")
		val INDOOR : Pair<String,String> = Pair("5", "0")
		val OUTDOOR : Pair<String,String> = Pair("5", "4")
		val SLOT1 : Pair<String,String> = Pair("1", "1")
		val SLOT2 : Pair<String,String> = Pair("1", "2")
		val SLOT3 : Pair<String,String> = Pair("1", "3")
		val SLOT4 : Pair<String,String> = Pair("4", "1")
		val SLOT5 : Pair<String,String> = Pair("4", "2")
		val SLOT6 : Pair<String,String> = Pair("4", "3")
		
	#]
		
	State s0 initial{
		[#ttAd = resources.ActuatorFactory().getActuatorAdapter(resources.ActuatorType.TROLLEY)#]
		println("trolleyController STARTS")
		run itunibo.planner.plannerUtil.loadRoomMapFromTxt("parkingMap.txt") 
		run itunibo.planner.plannerUtil.initAI()
		println("INITIAL MAP") 
 		run itunibo.planner.plannerUtil.showMap() 
		run itunibo.planner.plannerUtil.startTimer()
	}
	Goto idle
				  
	State idle{
		[# currentTask = "INDOOR" #]
		updateResource [# "trolley IDLE" #]
		println("trolleyController idle")
	}
	Transition t0 whenMsg moveToIn -> working
				  whenMsg moveToOut -> working
				  whenMsg moveToSlot -> working
				  whenMsg moveToHome -> working
				  whenMsg stop -> stopped 
	
	
	State working{
		printCurrentMessage
		println("trolley WORKING")
		updateResource [# "WORKING" #]
		
		//Move the trolley to the INDOOR area
		onMsg(moveToIn : moveToIn(WHERE)) {
				println("trolley trip to INDOOR start")
				[# 
					itunibo.planner.plannerUtil.planForGoal(INDOOR.first,INDOOR.second) 
					currentTask = "INDOOR"
			
					var mv : String = itunibo.planner.plannerUtil.getNextPlannedMove()
					while(! mv.equals("")){
						ttAd.sendCommand(mv) 
						delay(1500)
						itunibo.planner.plannerUtil.updateMap(mv)
						mv = itunibo.planner.plannerUtil.getNextPlannedMove()	
				}#]
				println("trolley trip to INDOOR end")
				updateResource [# "trolley in INDOOR" #]
		}
		//Move the trolley to the OUTDOOR area
		onMsg(moveToOut : moveToOut(WHERE)) {
				
				println("trolley trip to OUTDOOR start")
				[# 
					itunibo.planner.plannerUtil.planForGoal(OUTDOOR.first,OUTDOOR.second) 
					currentTask = "OUTDOOR"
			
					var mv : String = itunibo.planner.plannerUtil.getNextPlannedMove()
					while(! mv.equals("")){
						ttAd.sendCommand(mv) 
						delay(1500)
						itunibo.planner.plannerUtil.updateMap(mv)
						mv = itunibo.planner.plannerUtil.getNextPlannedMove()	
				}#]
				println("trolley trip to OUTDOOR end")
				updateResource [# "trolley in OUTDOOR" #]
				forward trolley_controller -m moveToHome : moveToHome(X)
		}
		//Move the trolley to the specific parking slot
		onMsg(moveToSlot : moveToSlot(SLOTNUM)) {
			[# 
				var SLOTNUM = payloadArg(0).toInt()
				if( currentTask.equals("INDOOR"))
					currentTask = "PARKIN"
				else
					currentTask = "PARKOUT"
			#]
			println("trolley trip to slot $SLOTNUM start")
			//updateResource [# "trolley moveToSlot($SLOTNUM)" #]
			
			[#
				when(SLOTNUM){
					1 -> itunibo.planner.plannerUtil.planForGoal(SLOT1.first, SLOT1.second)
					2 -> itunibo.planner.plannerUtil.planForGoal(SLOT2.first, SLOT2.second)
					3 -> itunibo.planner.plannerUtil.planForGoal(SLOT3.first, SLOT3.second)
					4 -> itunibo.planner.plannerUtil.planForGoal(SLOT4.first, SLOT4.second)
					5 -> itunibo.planner.plannerUtil.planForGoal(SLOT5.first, SLOT5.second)
					6 -> itunibo.planner.plannerUtil.planForGoal(SLOT6.first, SLOT6.second)
				}
				
				var mv = itunibo.planner.plannerUtil.getNextPlannedMove()
				while(! mv.equals("")){
						ttAd.sendCommand(mv)
						delay(1500)						
						itunibo.planner.plannerUtil.updateMap(mv)
						mv = itunibo.planner.plannerUtil.getNextPlannedMove()		
				}#]
			
		 	updateResource [# "trolley in slot $SLOTNUM" #] 
			println("trolley trip to slot $SLOTNUM end")
			println("trolley $currentTask")
			if [# currentTask.equals("PARKIN") #]{
				forward trolley_controller -m moveToHome : moveToHome(X)
			}
		}
		
		
		//Move the trolley to the HOME position
		onMsg(moveToHome : moveToHome(HOME)) {
				[#
				currentTask = "HOME"
				itunibo.planner.plannerUtil.planForGoal(HOME.first, HOME.second)
				var mv : String = itunibo.planner.plannerUtil.getNextPlannedMove()
				while(! mv.equals("")){
						ttAd.sendCommand(mv) 
						delay(1500)
						itunibo.planner.plannerUtil.updateMap(mv)
						mv = itunibo.planner.plannerUtil.getNextPlannedMove()
				}#]
				println("trolley in HOME")
				updateResource [# "trolley in HOME" #]
		
		}
	}
	Transition t1  
			whenMsg error -> blocked
			whenMsg stop -> stopped
	     	whenMsg moveToHome -> working
			whenMsg moveToIn -> working
			whenMsg moveToOut -> working
		 	whenMsg moveToSlot -> working
			whenMsg goToIdle -> idle
		
	State blocked{
		println("trolleyController blocked")
	}
	
	State stopped{
		println("trolleyController stopped")
	}
	Transition t2
		whenMsg resume and [# currentTask == "HOME" || currentTask == "IDLE" #]-> idle
		whenMsg resume and [# currentTask == "PARKIN" || currentTask == "PARKOUT" || currentTask == "OUTDOOR" || currentTask == "INDOOR"#] -> working
}


QActor fan context  ctxcarparking{
	[#var state : String = "OFF"
		#
	]
	State s0 initial{
		println("FAN STARTS")
	}
	Goto off
	
	State off{
		println("FAN OFF")
	}
	Transition t1
		whenMsg command -> checkCommand
		
	State checkCommand{
		onMsg(command : command(X)){
			[#
				if (payloadArg(0)=="ON"){
					state = "ON"
					#]
					updateResource [# "status ON" #]
					
					//replyTo command with executed : executed(ON)
					[# 
				}else{
					state = "OFF" 
				#]
				updateResource [# "status OFF" #]
					//replyTo command with executed : executed(OFF)
				[#
				}
				#]
				
				
		}
	}
	Goto off if [#state == "OFF"#] else on
	

	State on{
		println("FAN ON")
	}
	Transition t1
		whenMsg command -> checkCommand

		
}




/* ======================================================================== */

 